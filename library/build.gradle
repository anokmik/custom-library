apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

def baseName = "Beanstalk_SDK"
def version = "1.0.0"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        quiet true
        abortOnError false
        warningsAsErrors false
        noLines false
        showAll true
        htmlReport true
    }
    dexOptions {
        dexInProcess true
        javaMaxHeapSize "2g"
    }
    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            def extension = "aar"
            if (outputFile != null && outputFile.name.endsWith(extension)) {
                def fileName = "${baseName}_${version}.${extension}"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}